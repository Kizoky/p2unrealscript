//=============================================================================
// TimedEmitter
//
// Emits and then cleans up unless you call Refresh.
//=============================================================================
class TimedEmitter extends P2Emitter;

var float	PlayTime;			// how long we normally spark for, before we must be refreshed
var float	FinishUpTime;		// how long after StopSparking does it get destroyed.


///////////////////////////////////////////////////////////////////////////////
// Make sure to init your timer.
///////////////////////////////////////////////////////////////////////////////
function PostBeginPlay()
{
	Super.PostBeginPlay();
	SetTimer(PlayTime, false);
}

///////////////////////////////////////////////////////////////////////////////
// Time to destroy us
///////////////////////////////////////////////////////////////////////////////
function Timer()
{
	StopPlaying();
}

///////////////////////////////////////////////////////////////////////////////
// Resets before we get to our finish up state
///////////////////////////////////////////////////////////////////////////////
function Refresh()
{
	GotoState('');
	SetTimer(PlayTime, false);
}

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
function StopPlaying()
{
	local int i;

	for(i = 0; i<Emitters.Length; i++)
	{
		if(SuperSpriteEmitter(Emitters[i]) != None)
			SuperSpriteEmitter(Emitters[i]).AllowParticleSpawn = false;
		Emitters[i].RespawnDeadParticles = false;
	}
	GotoState('FinishingUp');
}

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
// FinishingUp--kill after a certain amount of time
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
state FinishingUp
{
	///////////////////////////////////////////////////////////////////////////////
	// stop all together
	///////////////////////////////////////////////////////////////////////////////
	function Timer()
	{
		// get rid of me
		Destroy();
	}

Begin:
	SetTimer(FinishUpTime, false);
//	log(self$" finishing up ");
}

defaultproperties
{
	PlayTime=0.5
	FinishUpTime=0.5
}
