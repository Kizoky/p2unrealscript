///////////////////////////////////////////////////////////////////////////////
// Marker that will force a notify every so often
///////////////////////////////////////////////////////////////////////////////
class BlipMarker extends TimedMarker;

///////////////////////////////////////////////////////////////////////////////
// VARS
///////////////////////////////////////////////////////////////////////////////

var bool bNotifyOnStart;	// Whether or not to notify as soon as you start (will decrement count, so be careful)
var int NotifyTime;		// frequency for notifies. Every this many seconds a notify is made
var int NotifyCount;	// how many times to notify before you die (<=0 for infinite)


///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
function PostBeginPlay()
{
	Super.PostBeginPlay();
	OriginActor = Owner;
}

///////////////////////////////////////////////////////////////////////////////
// perform notify
///////////////////////////////////////////////////////////////////////////////
function NotifyAndCount()
{
//	log(self$" telling people about myself, owner "$Owner$" ---notify controllers called "$NotifyCount);
	NotifyControllers();

	if(NotifyCount == 1)
	{
		Destroy();
		return;
	}
	else if(NotifyCount > 0)
		NotifyCount--;
}

///////////////////////////////////////////////////////////////////////////////
// Notify each time you're called
///////////////////////////////////////////////////////////////////////////////
function Timer()
{
	NotifyAndCount();
}

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
// Start sending out notifies
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
auto state Blipping
{
	function BeginState()
	{
		// trigger a notify on start
		if(bNotifyOnStart)
			NotifyAndCount();
		// and set it all in motion from now on
		SetTimer(NotifyTime, true);
	}
}

defaultproperties
{
	NotifyTime=3.0
	bNotifyOnStart=true
}
