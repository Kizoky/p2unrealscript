///////////////////////////////////////////////////////////////////////////////
// WeatherEffect
// Copyright 2014, Running With Scissors, Inc.
//
// A special Emitter class that's designed to follow the Dude around.
///////////////////////////////////////////////////////////////////////////////
class WeatherEffect extends Emitter
	abstract;

///////////////////////////////////////////////////////////////////////////////
// Vars, consts, enums etc.
///////////////////////////////////////////////////////////////////////////////
var() vector ViewOffset;		// How much to offset from player viewpoint. Should probably have a large positive Z value so it spawns high in the sky and rains down

var PlayerController Player;	// Player controller

///////////////////////////////////////////////////////////////////////////////
// Get player
///////////////////////////////////////////////////////////////////////////////
function GetPlayer()
{
	foreach DynamicActors(class'PlayerController', Player)
		break;
		
	if (Player == None)
		SetTimer(1.0, false);
	else
		SetOwner(Player);
}

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
event Timer()
{
	GetPlayer();
}

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
event PostBeginPlay()
{
	Super.PostBeginPlay();
	GetPlayer();
}

///////////////////////////////////////////////////////////////////////////////
// Follow the player around and make it rain only on them
///////////////////////////////////////////////////////////////////////////////
event Tick(float dT)
{
	Super.Tick(dT);
	if (Player != None)
		SetLocation(Player.ViewTarget.Location + ViewOffset);
}
	
///////////////////////////////////////////////////////////////////////////////
// Set default offset to high in the sky, so it rains down on the player
///////////////////////////////////////////////////////////////////////////////
defaultproperties
{
	ViewOffset=(Z=2000)
}