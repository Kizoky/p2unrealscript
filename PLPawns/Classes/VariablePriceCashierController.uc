///////////////////////////////////////////////////////////////////////////////
// VariablePriceCashierController
// Copyright 2014, Running With Scissors, Inc.
//
// A cashier controller that reads its price from the nearest PriceBoard.
///////////////////////////////////////////////////////////////////////////////
class VariablePriceCashierController extends FFCashierController;

///////////////////////////////////////////////////////////////////////////////
// Vars, consts, enums, etc.
///////////////////////////////////////////////////////////////////////////////
var PriceBoard MyPriceBoard;

///////////////////////////////////////////////////////////////////////////////
// PostBeginPlay
// Find the nearest PriceBoard
///////////////////////////////////////////////////////////////////////////////
event PostBeginPlay()
{
	local PriceBoard UseBoard;
	local float UseDist;
	
	UseDist = 999999.999999;
	
	foreach DynamicActors(class'PriceBoard', UseBoard)
	{
		if (VSize(Pawn.Location - UseBoard.Location) < UseDist)
		{
			UseDist = VSize(Pawn.Location - UseBoard.Location);
			MyPriceBoard = UseBoard;
		}
	}
	
	log(self@"initialized using price board"@MyPriceBoard);
}

///////////////////////////////////////////////////////////////////////////////
// The price of our goods is dictated by the nearby price board
///////////////////////////////////////////////////////////////////////////////
function float GetTotalCostOfYourProducts(P2Pawn CheckP, P2Pawn OrigOwner)
{
	if (MyPriceBoard != None)
		return MyPriceBoard.GetPrice();
	else
		warn(self@"HAS NO PRICE BOARD =========================================");
}

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
// ExchangeGoodsAndServices
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
state ExchangeGoodsAndServices
{
	///////////////////////////////////////////////////////////////////////////////
	// The customer doesn't decide how much to withdrawal or deposit, we secretly do
	// using values set in the cash register point
	///////////////////////////////////////////////////////////////////////////////
	function int DecideCustomerMoney()
	{
		if (MyPriceBoard != None)
			return MyPriceBoard.GetPrice();
		else
			warn(self@"HAS NO PRICE BOARD =========================================");
	}
}