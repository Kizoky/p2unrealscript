///////////////////////////////////////////////////////////////////////////////
// NapalmController
// Copyright 2002 RWS, Inc.  All Rights Reserved.
//
// Receptionist at the Free Health Clinic. She just tells you
// you have gonorrhea
//
///////////////////////////////////////////////////////////////////////////////
class NurseController extends CashierController;

///////////////////////////////////////////////////////////////////////////////
// Vars
///////////////////////////////////////////////////////////////////////////////
// User set vars

// Internal vars

///////////////////////////////////////////////////////////////////////////////
// Const
///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////
// Make her get mad and call the police, because we're walking back where we
// aren't supposed to be and we haven't paid
///////////////////////////////////////////////////////////////////////////////
function Trigger( actor Other, Pawn EventInstigator )
{
}

///////////////////////////////////////////////////////////////////////////////
// Like normal, except if you see the dude with his pants
// we don't mind
///////////////////////////////////////////////////////////////////////////////
function ActOnPawnLooks(FPSPawn LookAtMe, optional out byte StateChange)
{
	if(!LookAtMe.HasPantsDown())
		Super.ActOnPawnLooks(LookAtMe, StateChange);
}

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
// ExchangeWithDude
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
state ExchangeWithDude
{

WatchForCustomerReturn:
	// assigns statecount, we can't change states in a function that returns a variable
	CheckForCustomerProx();
	if(statecount == 2)
		Goto('Begin'); // he's ready to buy things again
	else if(statecount == 1)
	{
		Cleanup();
		GotoStateSave('WaitForCustomers');// he's too far away, so just wait on other customers
	}
	Sleep(1.0);
	Goto('WatchForCustomerReturn');

Begin:
	SetupCustomer();

	// With these cashiers, you don't have to bring the product to them, you go to them
	// and ask for it. They give the product and you give them the money.
	bResetInterests=false;

	// Test here for if you still have gonorrhea
	if(CustomerPawn.UrethraIsInfected())
		GotoState('WaitOnDudeToPay');
	else
		GotoState('DudeHasNoItem');
}

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
// DudeHasNoItem
// Used if the dude is cured and the nurse can't do anything with you
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
state DudeHasNoItem
{
Begin:
	if(CheckToGiggle())
		Sleep(SayTime);

	// nurse says hi
	TalkSome(MyPawn.myDialog.lGreeting,,true);
	PrintDialogue("Greeting");
	Sleep(SayTime);

	// dude says hi
	PrintDialogue(InterestPawn$" greeting ");
	TalkSome(CustomerPawn.myDialog.lGreeting, CustomerPawn,true);
	Sleep(SayTime);

	// nurse asks how she can help you
	PrintDialogue("can i help you?");
	TalkSome(MyPawn.myDialog.lCanIHelpYou);
	Sleep(SayTime);
	// return to handling the cash register
	GotoState('WaitForCustomers');
}

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
// WaitOnDudeToPay
// Only way to finish this is to unzip your pants before the nurse
// Then she'll advise you.
// You can only get into this state if your infected.
// Otherwise you use 
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
state WaitOnDudeToPay
{
	///////////////////////////////////////////////////////////////////////////////
	// Like normal, except if you see the dude with his pants
	// down, tell him to go to the vendacure
	///////////////////////////////////////////////////////////////////////////////
	function ActOnPawnLooks(FPSPawn LookAtMe, optional out byte StateChange)
	{
		if(P2Pawn(LookAtMe) != None
			&& P2Pawn(LookAtMe).HasPantsDown()
			&& LookAtMe.MyBodyFire == None)
		{
			GotoState('TakePaymentFromDude');
			StateChange=1;
		}
		else
			Super.ActOnPawnLooks(LookAtMe, StateChange);
	}

Begin:
	if(CheckToGiggle())
		Sleep(SayTime);

	// nurse says hi
	TalkSome(MyPawn.myDialog.lGreeting,,true);
	PrintDialogue("Greeting");
	Sleep(SayTime);

	// dude says hi
	PrintDialogue(InterestPawn$" greeting ");
	TalkSome(CustomerPawn.myDialog.lGreeting, CustomerPawn,true);
	Sleep(SayTime);

	// nurse asks how she can help you
	PrintDialogue("can i help you?");
	TalkSome(MyPawn.myDialog.lCanIHelpYou);
	Sleep(SayTime);

	// dude says he's got a burning feeling
	PrintDialogue(InterestPawn$" i've got a burning feeling ");
	TalkSome(CustomerPawn.myDialog.lDude_FreeHealth, CustomerPawn,true);
	Sleep(SayTime);

	// Nurse not sure...
	PrintDialogue("hmmm");
	SayTime = Say(MyPawn.myDialog.lHmm, bImportantDialog);
	Sleep(SayTime);

	// Nurse tells him to pull down his pants
	PrintDialogue("pull down your pants");
	SayTime = Say(MyPawn.myDialog.lNurse_CheckProblem, bImportantDialog);
	Sleep(SayTime);
}

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
// TakePaymentFromDude
// Dude gets looked at by a nurse
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
state TakePaymentFromDude
{
	///////////////////////////////////////////////////////////////////////////////
	// Unhook me from the dude controller
	///////////////////////////////////////////////////////////////////////////////
	function EndState()
	{
		Super.EndState();
		InterestPawn = None;
	}

Begin:
	// nurse says to go to the vendacure
	PrintDialogue("go to the vend-a-cure");
	TalkSome(MyPawn.myDialog.lNurse_Gonorrhea);
	Sleep(SayTime);

	// dude says thanks
	PrintDialogue(InterestPawn$" dude thanks");
	TalkSome(CustomerPawn.myDialog.lThanks, CustomerPawn);
	Sleep(SayTime);

	// return to handling the cash register
	GotoState('WaitForCustomers');
}

defaultproperties
{
//	GameHint=""
}